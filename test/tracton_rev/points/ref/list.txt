10 REM THIS PROGRAM COMPUTES N EQUALLY SPACED
20 REM POINTS ON THE CIRCUMFERENCE OF A CIRCLE
30 PRINT "CENTER OF CIRCLE (X0,Y0) = ";
40 INPUT X0,Y0
50 PRINT "ANGLE IN DEGREES OF 1ST POINT = ";
60 INPUT W
70 LET W = (W*3.14159)/180
80 PRINT "NUMBER OF POINTS DESIRED = ";
90 INPUT N
100 PRINT "RADIUS OF CIRCLE = ";
110 INPUT R
120 LET Z = 6.28319/N
130 PRINT
140 PRINT "COORDINATES"
150 FOR I = 0 TO N - 1
160 LET X = X0 + (R*COS(W + (Z*I)))
170 LET Y = Y0 + (R*SIN(W + (Z*I)))
180 LET P = I + 1
190 PRINT "POINT:";P,"X = ";X,"Y = ";Y
200 NEXT I
210 PRINT
220 PRINT "TYPE 1 TO CONTINUE, 0 TO STOP"
230 INPUT L
240 IF L = 1 THEN 260
250 STOP
260 PRINT
270 GOTO 30
280 END

