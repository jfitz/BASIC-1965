10 REM THIS PROGRAM IS A BUBBLE SORT, WHICH PLACES THE
20 REM VALUES IN AN ARRAY IN ASCENDING SEQUENCE
30 REM IT IS INTENDED TO BE AN EXAMPLE AND NOT A
40 REM SPECIFIC CASE
50 REM THIS PROGRAM MAY BE CONVERTED INTO A SUBROUTINE
60 REM FOR USE IN A LARGER PROGRAM
70 REM THE ARRAY IS Z OF LENGTH N
80 REM Z IS ASSUMED TO HAVE BEEN DECLARED IN A DIM
90 REM STATEMENT AND N SET AHEAD OF TIME
100 LET N = 100
102 OPTION MAX_DIM N
110 PRINT "BUBBLE SORT"
112 PRINT N;" VALUES"
114 PRINT
120 DIM Z(100)
122 ARR Z = RND1(N, 100)
130 PRINT "ORIGINAL ARRAY:"
132 ARR PRINT Z
134 PRINT
140 GOSUB 1000
150 PRINT "SORTED ARRAY:"
152 ARR PRINT Z
154 PRINT
190 STOP
1000 LET I = N - 1
1010 FOR J = 0 TO I
1020 LET K = J + 1
1030 FOR L = N TO K STEP -1
1040 IF Z(L) > Z(J) THEN 1060
1050 LET Z(L), Z(J) = Z(J), Z(L)
1060 NEXT L
1070 NEXT J
1080 RETURN
9999 END

