10 REM THIS PROGRAM COMPUTES THE GROWTH PATTERN BETWEEN
20 REM A QUANTITY D THE DESTROYER AND A QUANTITY C
30 REM THE CREATOR. CREATORS CAN PROPOGATE, AND ON A
40 REM CHANCE METTING BETWEEN CREATOR AND DESTROYER,
50 REM THE CREATOR IS DESTROYED, AND THE DESTROYER
55 REM PROPAGATES
60 INPUT "NUMBER OF DESTROYERS: ";D
70 INPUT "NUMBER OF CREATORS: ";C
80 INPUT "BIRTH RATE OF CREATORS: ";Z
90 INPUT "CHANCE OF MEETING BETWEEN CREATOR AND DESTROYER: ";A
100 INPUT "TIME BETWEEN SAMPLES: ";H
110 INPUT "MAXIMUM GENERATIONS: ";G9
120 LET G = 0
130 PRINT
140 PRINT "INTERACTIVE GROWTH PATTERNS"
150 PRINT
160 PRINT "CREATOR","DESTROYER"
170 IF G = G9 THEN 470
180 LET G = G + 1
190 REM POSSIBLE ENCOUNTERS
200 LET P = C*D*A
210 REM CREATOR BIRTHS, DEATHS, AND NET CHANGE
220 LET C0 = C*Z*H
230 LET C2 = P*H
240 LET C5 = C0 - C2
250 REM NEXT GENERATION OF CREATORS
260 LET C9 = C + C5
270 IF C9 >= 0 THEN 290
280 LET C9 = 0
290 LET C = INT(C9)
300 REM DESTROYER BIRTHS, DEATHS, AND NET CHANGE
310 LET D0 = P*H
320 LET D2 = D/2
330 LET D5 = D0 - D2
340 REM NEXT GENERATION OF DESTROYERS
350 LET D9 = D + D5
360 IF D9 >= 0 THEN 380
370 LET D9 = 0
380 LET D = INT(D9)
400 PRINT C,D
410 IF C > 0 THEN 170
420 IF D > 0 THEN 170
430 PRINT "ALL GROWTH ENDED"
440 PRINT "NUMBER OF GENERATIONS:";G
450 PRINT
460 GOTO 560
470 PRINT "MAXIMUM NUMBER OF GENERATIONS REACHED"
480 PRINT
490 PRINT "FOR FURTHER GENERATIONS TYPE 1, IF NOT 0"
500 INPUT W
510 IF W = 1 THEN 530
520 GOTO 550
530 INPUT "MAXIMUM GENERATIONS: ";G9
540 GOTO 170
550 PRINT
560 PRINT "FOR NEXT PATTERN TYPE 1, 0 TO STOP"
570 INPUT W
580 IF W = 1 THEN 600
590 STOP
600 PRINT
610 GOTO 60
620 END
