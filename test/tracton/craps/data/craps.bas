10 REM THIS PROGRAM SIMULATES THE GAME OF CRAPS
20 RANDOMIZE
30 PRINT " FOR RULES, TYPE RULES, OTHERWISE TYPE GO"
40 INPUT R$
50 IF R$ = "RULES" THEN 90
60 IF R$ = "GO" THEN 140
70 PRINT "INVALID COMMAND"
80 GOTO 30
90 PRINT "A 7 OR 11 ON THE FIRST THROW WINS"
100 PRINT "YOU CAN ALSO WIN BY THROWING A 4,5,6,8,9,10"
110 PRINT "AND MATCHING IT BEFORE THROWING A 7. IF ON"
120 PRINT "THE FIRST THROW 2,3 OR A 12 COMES UP"
130 PRINT "YOU LOSE AUTOMATICALLY"
140 LET J = 0
150 GOSUB 460
160 LET D1 = N
170 GOSUB 460
180 LET D2 = N
190 LET D3 = D1 + D2
200 LET J = J + 1
210 IF J = 1 THEN 260
220 IF D3 = D4 THEN 420
230 IF D3 = 7 THEN 440
240 PRINT "YOU THROW A ";D3
250 GOTO 150
260 IF D3 = 7 THEN 320
270 IF D3 = 2 THEN 340
280 IF D3 = 3 THEN 340
290 IF D3 = 12 THEN 340
300 IF D3 = 11 THEN 320
310 LET D4 = D3
315 GOTO 240
320 PRINT "YOU THROW A ";D3;" YOU WIN"
330 GOTO 350
340 PRINT "YOU THROW A ";D3;" YOU LOSE"
350 PRINT
360 PRINT "TO CONTINUE TYPE Y, IF NOT TYPE N"
370 INPUT L$
380 IF L$ = "Y" THEN 400
390 STOP
400 PRINT
410 GOTO 30
420 PRINT "YOU THROW A ";D3;"YOU WIN BY MATCHING"
430 GOTO 350
440 PRINT "YOU THROW A ";D3;" SORRY YOU LOSE"
450 GOTO 350
460 LET N = 1 + INT(6*RND)
470 RETURN
480 END
