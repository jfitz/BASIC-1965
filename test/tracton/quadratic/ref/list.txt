10 REM THIS PROGRAM COMPUTES THE SOLUTION TO A
20 REM QUADRATIC EQUATION
30 PRINT "ENTER VALUES FOR A, B AND C"
40 INPUT A,B,C
50 LET D = ((B^2) - (4*A*C))/(4*A^2)
60 IF D > = 0 THEN 130
70 LET X = -B/(2*A)
80 LET Y = (SQR((4*A*C) - B^2))/(2*A)
90 PRINT "ROOTS ARE COMPLEX"
100 PRINT "REAL PART = ";X
110 PRINT "IMAGINARY PART = ";Y
120 GOTO 220
130 LET E = - B/(2*A)
140 IF E > = 0 THEN 170
150 LET Z = E - SQR(D)
160 GOTO 180
170 LET Z = E + SQR(D)
180 LET W = C/(Z*A)
190 PRINT "ROOTS ARE REAL"
200 PRINT "1ST ROOT = ";Z
210 PRINT "2ND ROOT = ";W
220 PRINT "********************"
230 PRINT "TYPE 1 TO CONTINUE, 0 TO STOP"
240 INPUT L
250 IF L = 1 THEN 270
260 STOP
270 PRINT
280 GOTO 30
290 END

