10 REM THIS PROGRAM COMPUTES THE SOLUTION TO
20 REM A SYSTEM OF 3 EQUATIONS IN 3 UNKNOWNS
30 PRINT "SOLVE FOR 3 EQUATIONS IN 3 UNKNOWNS"
40 PRINT "OF THE TYPE AX + BY + CZ = D"
50 PRINT "ENTER THE 1ST EQUATION (A,B,C,D)"
60 INPUT A1, B1, C1, D1
70 PRINT "ENTER THE 2ND EQUATION (A,B,C,D)"
80 INPUT A2, B2, C2, D2
90 PRINT "ENTER THE 3RD EQUATION (A,B,C,D)"
100 INPUT A3, B3, C3, D3
110 LET E1 = ((B1 * A2) / A1) - B2
120 LET E2 = ((C1 * A2) / A1) - C2
130 LET E3 = ((B1 * A3) / A1) - B3
140 LET E4 = ((C1 * A3) / A1) - C3
150 LET E5 = ((E1 * E4) - (E2 * E3))
160 IF E5 = 0 THEN 250
170 LET E6 = ((D1 * A2) / A1) - D2
180 LET E7 = ((D1 * A3) / A1) - D3
190 LET Y = ((E6 * E4) - (E2 * E7)) / E5
200 LET Z = ((E1 * E7) - (E6 * E3)) / E5
210 LET X = (D1 / A1) - (B1 / A1 * Y) - ((C1 / A1) * Z)
220 PRINT "SOLUTION"
230 PRINT "X = "; X, "Y = "; Y, "Z = "; Z
240 GOTO 260
250 PRINT "INSUFFICIENT OR ERRONEOUS DATA ENTERED"
260 PRINT
270 PRINT "TYPE 1 TO CONTINUE, 0 TO STOP"
280 INPUT L
290 IF L = 1 THEN 310
300 STOP
310 PRINT
320 GOTO 30
330 END
