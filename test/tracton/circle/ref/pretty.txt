10 REM THIS PROGRAM COMPUTES THE CENTER AND RADIUS
20 REM OF A CIRCLE FROM 3 NON-COLINEAR POINTS
30 PRINT "INPUT X1,Y1";
40 INPUT X1, Y1
50 PRINT "INPUT X2,Y2";
60 INPUT X2, Y2
70 PRINT "INPUT X3,Y2";
80 INPUT X3, Y3
90 LET A = (Y2 - Y1) / (X2 - X1)
100 LET B = (Y3 - Y1) / (X3 - X1)
110 LET C = ((X2 - X1) * (X2 + X1)) + ((Y2 - Y1) * (Y2 + Y1))
120 LET D = C / (2 * (X2 - X1))
130 LET E = ((X3 - X1) * (X3 + X1)) + ((Y3 - Y1) * (Y3 + Y1))
140 LET F = E / (2 * (X3 - X1))
150 LET Y0 = (F - D) / (B - A)
160 LET X0 = F - (B * Y0)
170 LET R = SQR((X3 - X0) ^ 2 + (Y3 - Y0) ^ 2)
180 PRINT "CENTER X0,Y0 = "; X0; ","; Y0
190 PRINT "RADIUS = "; R
200 PRINT
210 PRINT "TO CONTINUE TYPE 1, IF NOT TYPE 0"
220 INPUT L
230 IF L = 1 THEN 250
240 STOP
250 PRINT
260 GOTO 30
270 END
