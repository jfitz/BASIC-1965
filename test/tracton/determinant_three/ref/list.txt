10 REM THIS PROGRAM COMPUTES 3 X 3 MATRIX OPERATIONS
20 PRINT "ENTER 3 X 3 MATRIX"
30 INPUT A,B,C,D,E,F,G,H,I
40 REM COMPUTE DETERMINANT OF 3X3
50 LET M = (A*E*I) + (B*F*G) + (C*H*D)
60 LET N = M - (C*E*G) - (F*H*A) - (I*D*B)
70 REM COMPUTE INVERSE
80 LET O = ((E*I) - (H*F))/N
90 LET P = ((G*F) - (D*I))/N
100 LET Q = ((D*H) - (G*E))/N
110 LET R = ((H*C) - (B*I))/N
120 LET S = ((A*I) - (G*C))/N
130 LET T = ((G*B) - (A*H))/N
140 LET U = ((B*F) - (E*C))/N
150 LET V = ((D*C) - (A*F))/N
160 LET W = ((A*E) - (D*B))/N
170 PRINT "ORIGINAL MATRIX"
180 PRINT A,B,C
190 PRINT D,E,F
200 PRINT G,H,I
210 PRINT
220 PRINT "DETERMINANT = ";N
230 PRINT
240 PRINT "INVERSE OF MATRIX"
250 PRINT O,R,U
260 PRINT P,S,V
270 PRINT Q,T,W
280 PRINT
290 PRINT "TYPE 1 TO CONTINUE, 0 TO STOP"
300 INPUT L
310 IF L = 1 THEN 20
320 STOP
330 END
