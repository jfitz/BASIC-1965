BASIC-1965 interpreter version -1

10 REM THIS PROGRAM COMPUTES THE GROWTH PATTERN BETWEEN
20 REM A QUANTITY Y THE DESTROYER AND A QUANTITY X
30 REM THE CREATOR. X CAN PROPOGATE, AND ON A
40 REM CHANCE METTING BETWEEN X AND Y, X IS DESTROYED,
50 REM THUS INCREASING THE NUMBER OF YS.
60 PRINT "NUMBER OF DESTROYERS (Y) = ";
70 INPUT Y
80 PRINT "NUMBER OF CREATORS (X) = ";
90 INPUT X
100 PRINT "PROPAGATION RATE OF X = ";
110 INPUT Z
120 PRINT "CHANCE OF METTING BETWEEN X AND Y = ";
130 INPUT A
140 PRINT "TIME BETWEEN SAMPLES = ";
150 INPUT H
160 PRINT "MAXIMUM GENERATIONS = ";
170 INPUT K
180 LET J = 0
190 PRINT
200 PRINT "INTERACTIVE GROWTH PATTERNS"
210 PRINT
220 PRINT "CREATOR", "DESTROYER"
230 IF J = K THEN 470
240 LET J = J + 1
250 LET B = A * X * Y
260 LET C = ((Y - B) * H) + Y
270 IF C < 0 THEN 390
280 LET Y = C
290 LET D = (((X * Z) - B) * H) + X
300 IF D < 0 THEN 410
310 LET X = D
320 LET M = INT(X)
330 LET N = INT(Y)
340 PRINT M, N
350 IF M = 0 THEN 370
360 GOTO 230
370 IF N = 0 THEN 430
380 GOTO 230
390 LET Y = 0
400 GOTO 290
410 LET X = 0
420 GOTO 320
430 PRINT "ALL GROWTH ENDED"
440 PRINT "NUMBER OF GENERATIONS = "; J
450 PRINT
460 GOTO 560
470 PRINT "MAXIMUM NUMBER OF GENERATIONS REACHED"
480 PRINT
490 PRINT "FOR FURTHER GENERATIONS TYPE 1, IF NOT 0"
500 INPUT W
510 IF W = 1 THEN 530
520 GOTO 560
530 PRINT "MAXIMUM GENERATIONS = ";
540 INPUT K
550 GOTO 230
560 PRINT
570 PRINT "FOR NEXT PATTERN TYPE 1, 0 TO STOP"
580 INPUT W
590 IF W = 1 THEN 610
600 STOP
610 PRINT
620 GOTO 60
630 END

BASIC-1965 ended
