BASIC-1965 interpreter version -1

10 REM THIS PROGRAM COMPUTES PERMUTATIONS
20 REM AND COMBINATIONS, WHERE N = NUMBER OF
30 REM ITEMS AND R = SIZE OF GROUP SELECTED
40 PRINT "ENTER N,R";
50 INPUT N,R
60 IF N < R THEN 270
70 IF R < 0 THEN 290
80 LET T = N
90 GOSUB 310
100 LET A = T
110 LET T = N - R
120 GOSUB 310
130 LET B = T
140 LET P = A/B
150 LET T = R
160 GOSUB 310
170 LET C = A/(T*B)
180 PRINT "PERMUTATIONS = ";P
190 PRINT "COMBINATIONS = ";C
200 PRINT
210 PRINT "TYPE 1 TO CONTINUE, 0 TO STOP"
220 INPUT L
230 IF L = 1 THEN 250
240 STOP
250 PRINT
260 GOTO 40
270 PRINT "N < R, INVALID INPUT"
280 GOTO 40
290 PRINT "R < 0, INVALID INPUT"
300 GOTO 40
310 IF T = 0 THEN 390
320 IF T = 1 THEN 390
330 LET J = 1
340 FOR I = 2 TO T
350 LET J = J*I
360 NEXT I
370 LET T = J
380 GOTO 400
390 LET T = 1
400 RETURN
410 END

BASIC-1965 ended
