100 DIM P(20, 20), M(20, 20), Z(20, 20), A(1, 20), B(1, 20)
110
120 READ N
130 MAT READ P(N, N)
140 MAT Z = IDN(N, N)
150
160 PRINT "TRANSITION PROBABILITIES"
170 PRINT
180 MAT PRINT P
190
200 MAT Z = Z - P
210 FOR I = 1 TO N
220 LET Z(I, N) = 1
230 NEXT I
240 MAT M = INV(Z)
245 MAT A = ZER(1, N)
250 FOR J = 1 TO N
260 LET A(1, J) = M(N, J)
270 NEXT J
280 PRINT "LIMITING PROBABILITIES"
290 PRINT
300 MAT PRINT A
310
320 MAT M = IDN(N, N)
330 MAT M = M - P
340 FOR I = 1 TO N
350 FOR J = 1 TO N
360 LET M(I, J) = M(I, J) + A(1, J)
370 NEXT J
380 NEXT I
390 MAT Z = INV(M)
400
410 FOR I = 1 TO N
420 FOR J = 1 TO N
430 LET M(I, J) = (Z(J, J) - Z(I, J)) / A(1, J)
440 NEXT J
450 NEXT I
460 PRINT "MEAN PASSAGE TIMES"
470 PRINT
480 MAT PRINT M
490
500 MAT B = A * M
510 PRINT "FIRST PASSAGE TIME IN EQUILIBRIUM"
520 PRINT
530 MAT PRINT B
540
550 DATA 3
560 DATA 0.5, 0.25, 0.25
570 DATA 0.5, 0, 0.5
580 DATA 0.25, 0.25, 0.5
999 END

