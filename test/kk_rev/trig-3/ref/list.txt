100 REM ANGLE-SIDE-SIDE
110 OPTION TRIG_REQUIRE_UNITS TRUE
135 DEF FNR(X) = INT(100*X+0.5)/100
140 PRINT "ANGLE", "SIDE", "SIDE", "THIRD SIDE", "CASE 2"
150 READ B,X,Y
160 PRINT B,X,Y,
170 LET S = X*SIN(B)/Y
180 IF S > 1 THEN 290
190 IF FNR(S) = 1 THEN 320
195
200 REM TWO POINTS OF INTERSECTION
210 LET T = S/SQR(1-S^2)
220 LET C = DEG(ATN(T))
230 LET A = 180{DEG} - B - C
240 GOSUB 370
250 LET C = 180{DEG} - C
260 LET A = 180{DEG} - B - C
270 GOSUB 370
275 PRINT
280 GOTO 150
285
290 REM NO POINT OF INTERSECTION
300 PRINT "NONE", "NONE"
310 GOTO 150
315
320 REM ONE POINT OF INTERSECTION
330 LET A = 90{DEG} - B
340 GOSUB 370
350 PRINT "NONE"
360 GOTO 150
365
370 REM ANGLE-SIDE-ANGLE ROUTINE
380 LET Z = X*SIN(A)/S
390 IF Z >= 0 THEN 420
400 PRINT "NONE",
410 RETURN
420 PRINT Z,
430 RETURN
440
450 DATA 60{DEG},10,8, 60{DEG},10,9, 60{DEG},10,11, 120{DEG},10,8, 120{DEG},10,9
 WARNING: Line exceeds LIST width limit 72
 WARNING: Line exceeds PRETTY width limit 72
460 DATA 120{DEG},10,11, 90{DEG},3,5, 60{DEG},5,5, 30{DEG},10,5
470 END

