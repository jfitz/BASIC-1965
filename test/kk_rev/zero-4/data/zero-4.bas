10 DEF FNF(X) = X^5 + 2*X^3 -1
11 REM DEF FNF(X) = -3*X^5 + X^3 + 1
12 REM DEF FNF(X) = X^2 + 1
13 REM DEF FNF(X) = X^2 - 0.001
14 REM DEF FNF(X) = SIN(X) - LOG(X)/LOG(10)
20 OPTION REQUIRE_INITIALIZED TRUE
30 READ A,B
100 LET S = SGN(FNF(A))
110 LET T = SGN(FNF(B))
120 IF S*T = 0 THEN 300
130 IF S*T > 0 THEN 200
140 GOTO 600
200 LET L = B-A
210 FOR I = 1 TO 1000
220 LET X = A + RND()*L
230 IF S * SGN(FNF(X)) >= 0 THEN 240
235 BREAK
240 NEXT I
245 IF I < 1000 THEN 270
250 PRINT "NO CHANGE OF SIGN FOUND"
260 STOP
270 LET B = X
280 GOTO 600
300 IF S = 0 THEN 330
310 LET X = B
320 GOTO 500
330 LET X = A
340 GOTO 500
500 PRINT "ONE ZERO AT" X
510 STOP
600 LET X(1+S) = A
610 LET X(1-S) = B
620 LET X = (X(0)+X(2))/2
630 LET F = SGN(FNF(X))
640 IF F = 0 THEN 800
650 LET X(1+F) = X
670 IF ABS(X(0)-X(2))/(ABS(X(0))+ABS(X(2))) < 0.0000005 THEN 800
680 GOTO 620
800 PRINT "ONE ZERO AT" X
810 STOP
900 REM DATA 0,1
910 DATA 0,1
999 END

