10 REM GALILEO'S CIRCLE BECOMES A STRAIGHT LINE
20 REM FROM DISCOURSE ON TWO SCIENCES
50 REM CALCULATE LENGTH OF LINE (X1,X2)-(Y1,Y2)
51 DEF FNL(X1, X2, Y1, Y2) = SQR((X1 - Y1) ^ 2 + (X2 - Y2) ^ 2)
60 REM CONVERT DEGREES TO RADIANS
61 DEF FNR(D) = D / 180 * 3.14159
100 REM POINT A
101 READ A1, A2
110 REM CIRCLE AT POINT D WITH RADIUS DC
111 READ D1, D2
113 READ C1, C2
115 LET D5 = D1 - C1     ' RADIUS
121 READ Z9, Z8
122 READ R9
200 REM FIND POINT B
201 LET B1 = (A1 + D1) / 2
202 LET B2 = 0
203 LET I1 = D1 + D5
204 LET I2 = 0
217 PRINT "IDENTIFYING POINT B"
218 PRINT "B", "AC/BC", "AI/BI", "Z"
219 LET Z = 1
220 REM RATIO OF AC/BC
221 LET F1 = FNL(A1, A2, C1, C2)
222 LET F2 = FNL(B1, B2, C1, C2)
223 LET R0 = F1 / F2
230 REM RATIO OF AI/BI
231 LET G1 = FNL(A1, A2, I1, I2)
232 LET G2 = FNL(B1, B2, I1, I2)
233 LET R1 = G1 / G2
239 PRINT B1, R0, R1, Z
240 IF ABS(R0 - R1) < R9 THEN 300
241 IF R0 < R1 THEN 245
242 LET B1 = B1 + Z
243 GOTO 246
245 LET B1 = B1 - Z
246 LET Z = Z * Z8
247 IF Z > Z9 THEN 220
248 PRINT "CANNOT RESOLVE POINT B"
249 STOP
300 REM PRINT DEFINED VALUES
301 PRINT "POINT C    :"; C1; C2
302 PRINT "POINT B    :"; B1; B2
303 PRINT "DISTANCE BC:"; F2
304 PRINT "POINT A    :"; A1; A2
305 PRINT "DISTANCE AC:"; F1
306 PRINT "RATIO AC/BC:"; R0
400 REM PICK VALUES ON CIRCLE D
401 PRINT "ANGLE", "POINT", " ", "DISTANCES", " ", "RATIO", "DIFFERENCE"
410 FOR I = 0 TO 365 STEP 5
420 REM PICK POINT ON CIRCLE D
421 LET T = FNR(I)
422 LET I1 = D1 + COS(T) * D5
423 LET I2 = D2 + SIN(T) * D5
424 LET G1 = FNL(A1, A2, I1, I2)
425 LET G2 = FNL(B1, B2, I1, I2)
426 LET R1 = G1 / G2
430 REM PRINT RESULTS
431 PRINT T, I1; I2, G1; G2, R1, R1 - R0
441 NEXT I
900 REM DATA - POINT A, POINT D, POINT C
901 DATA 0, 0, 100, 0, 75, 0, 1E-05, 0.995, 1E-05
999 END

