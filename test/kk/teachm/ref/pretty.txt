BASIC-1965 interpreter version -1

0 REM  * TEACHM  *
10 PRINT "BASIC IS A SIMPLE LANGUAGE. IT ENABLES ONE TO DESCRIBE"
30 PRINT "PROGRAMS TO THE COMPUTER WITH LITTLE LEARNING EFFORT."
40 PRINT "THIS PROGRAM WILL TRY TO TEACH YOU HOW TO PROGRAM IN BASIC."
99
100 GOSUB 8100
105 PRINT "BEFORE WE CAN HAVE THE COMPUTER WORK WITH DATA, WE MUST HAVE"
110 PRINT "THE DATA READ WITH A -----STATEMENT. "
120 PRINT "(1. READ  2. PRINT  3. DATA  4. NONE OF THESE.)"
130 LET C = 1
140 GOSUB 8000
150 IF X=C THEN 200
160 PRINT "LET'S TRY THAT ONE AGAIN."
170 GOTO 100
199
200 GOSUB 8100
205 PRINT "ALL DATA THAT IS READ IN USING A READ STATEMENT MUST APPEAR"
210 PRINT "IN SOME ----- STATEMENT.  (1. INPUT  2. PRINT  3. DATA"
215 PRINT "4. NONE OF THESE.)"
220 LET C = 3
230 GOSUB 8000
240 IF C=X THEN 300
250 IF X=1 THEN 280
260 PRINT "ANOTHER RETRY -- OKAY?"
270 GOTO 200
280 PRINT "THE INPUT STATEMENT IS SIMILAR TO THE READ STATEMENT, BUT"
285 PRINT "CALLS FOR THE INPUT TO BE MADE FROM THE TELETYPE MACHINE"
290 PRINT "RATHER THAN FROM DATA STATEMENTS.  READ AND DATA GO"
292 PRINT "TOGETHER."
295 GOTO 100
300 GOSUB 8100
305 PRINT "TO PERFORM A COMPUTATION, WE HAVE A VARIABLE ON THE LEFT OF"
310 PRINT "THE EQUAL SIGN, AND A FORMULA ON THE RIGHT E.G., TO "
315 PRINT " ------ X = A + B   .  (1. COMPUTE  2. LET  3. VARIABLE"
340 PRINT "4. NONE OF THESE.)"
350 LET C = 2
360 GOSUB 8000
370 IF C=X THEN 400
375 IF X<>1 THEN 390
380 PRINT "BASIC USES THE SIMPLER WORD  LET  RATHER THAN COMPUTE."
385 GOTO 300
390 PRINT "LET'S TRY THAT ONE AGAIN."
395 GOTO 300
400 GOSUB 8100
405 PRINT "ONE OF THE FOLLOWING DOES NOT FOLLOW THE RULES. WHICH ONE?"
410 PRINT "(1. LET X = Y  2. LET Z = Z + 17  3. LET A + B = X )"
420 LET C = 3
430 GOSUB 8000
440 IF C=X THEN 500
450 IF X=1 THEN 470
460 PRINT "THIS FORM IS PERFECTLY CORRECT.  IT CALLS FOR COMPUTING THE"
465 PRINT "NUMBER Z+17, AND REPLACING THE OLD VALUE OF Z WITH THIS VALUE"
466 PRINT "THUS, Z IS INCREASED BY 17 UNITS."
467 GOTO 400
470 PRINT "THIS IS OKAY, WS SIMPLY HAVE X TAKE ON THE VALUE OF Y."
480 GOTO 400
499
500 GOSUB 8100
505 PRINT "TO INCREASE THE VALUE OF THE VARIABLE T BY X UNITS, WHICH OF"
510 PRINT "THE FOLLOWING DO WE USE?"
520 PRINT "(1. LET T = X + T  2. LET X = T + X  3. LET T - X = T )"
530 LET C = 1
540 GOSUB 8000
550 IF C=X THEN 600
560 IF C=3 THEN 580
570 PRINT "THIS ONE INCREASES THE VALUE OF X BY T UNITS."
575 GOTO 500
580 PRINT "THIS FORM IS ILLEGAL. LET'S BACK UP A BIT."
590 GOTO 400
599
600 GOSUB 8100
605 PRINT "YOU HAVE COMPLETED YOUR FIRST COURSE IN BASIC."
610 PRINT "CONGRATULATIONS."
620 STOP
8000 PRINT "YOUR ANSWER";
8010 INPUT X
8020 IF C<>X THEN 8050
8030 PRINT "CORRECT"
8040 GOTO 8060
8050 PRINT "INCORRECT. CORRECT ANSWER IS "; C
8060 RETURN
8099
8100 PRINT
8110 PRINT
8120 RETURN
8199
9999 END

BASIC-1965 ended
