100 LET X1 = 0
110 LET X2 = 0
120 LET Y1 = 0
130 LET Y2 = 0
140 LET Z = 0
150
160 READ N
170 FOR I = 1 TO N
180 READ X, Y
190 LET X1 = X1 + X
200 LET Y1 = Y1 + Y
210 LET X2 = X2 + X * X
220 LET Y2 = Y2 + Y * Y
230 LET Z = Z + X * Y
240 NEXT I
250
260 LET X3 = X1 / N
265 LET Y3 = Y1 / N
270 LET S1 = X2 - X1 * X3
280 LET S2 = Y2 - Y1 * Y3
290 LET S = Z - X1 * Y3
300 LET B = S / S1
310 LET B1 = Y3 - B * X3
320 LET S3 = S2 - B * S
330 LET S4 = S3 / (N - 2)
340
350 PRINT "NUMBER = " N
360 PRINT "MEAN OF X = " X3, "OF Y " Y3
370 PRINT "SLOPE = " B, "Y-INTERCEPT = " B1
375 PRINT
380 PRINT "SUM-OF-SQUARES ", "TOTAL", Y2
390 PRINT "    ", "MEAN", Y3 * Y1
400 PRINT "    ", "SLOPE", B * S
410 PRINT "    ", "RESIDUAL", S3
415 PRINT
420 PRINT "STANDARD DEVIATIONS"
430 PRINT "    ", "X  ", SQR(S1 / (N - 1))
440 PRINT "    ", "Y  ", SQR(S2 / (N - 1))
450 PRINT "    ", "ERROR", SQR(S4)
460 PRINT "    ", "Y-BAR", SQR(S4 / N)
470 PRINT "    ", "SLOPE", SQR(S4 / S1)
480 PRINT "    ", "Y-INTER.", SQR(S4 * (1 / N + X3 * X3 / S1))
485 PRINT
490 PRINT "F-RATIO FOR SLOPE = " B * S / S4
500 PRINT "CORRELATION COEFFICIENT = " S / SQR(S1 * S2)
510
900 DATA 5
901 DATA 480, 56
902 DATA 500, 61
903 DATA 520, 78
904 DATA 540, 71
905 DATA 560, 82
999 END

