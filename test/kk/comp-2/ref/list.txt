5  PRINT
10 PRINT "A, B, P, R, Y, T";
20 INPUT A, B, P, R, Y, T
21 IF A < 0 THEN 800
22 IF B < 0 THEN 800
23 IF R <= 0 THEN 800
24 IF Y <= 0 THEN 800
25 IF T <= 0 THEN 800
26 IF T = 999 THEN 9999
30 IF A = 999 THEN 100
40 IF B = 999 THEN 200
50 IF P = 999 THEN 300
60 IF Y = 999 THEN 400
70 IF R = 999 THEN 500
80
100 REM FIND ORIGINAL AMOUNT
110 GOSUB 1000
120 LET A = (B+P1)/R2 - P1
125 IF A < 0 THEN 850
130 PRINT "ORIGINAL AMOUNT = $" A
140 GOTO 5
150
200 REM FIND FINAL AMOUNT
210 GOSUB 1000
220 LET B = (A+P1)*R2 - P1
225 IF B < 0 THEN 850
230 PRINT "FINAL AMOUNT = $" B
240 GOTO 5
250
300 REM FIND PAYMENT
310 GOSUB 1000
320 LET P = R1*(B - A*R2)/(R2-1)
330 PRINT "PAYMENT = $" P
340 GOTO 5
350
400 REM FIND NUMBER OF YEARS
410 GOSUB 1000
415 LET Q = (B+P1)/(A+P1)
416 IF Q <= 1 THEN 850
420 LET Y = LOG(Q)/LOG(1+R1)/T
430 PRINT "NUMBER OF YEARS = " Y
440 GOTO 5
450
500 REM FIND ANNUAL INTEREST
505 LET S = B - (A+Y*T*P)
510 FOR R = 0.1 TO 100 STEP 0.1
 WARNING: FORNEXT length exceeds limit 40
520    GOSUB 1000
530    LET S1 = B - ((A+P1)*R2 - P1)
535    IF S*S1 <= 0 THEN 550
536    LET S = S1
540 NEXT R
545 PRINT "NO ANSWER FOUND"
546 GOTO 5
550 PRINT "ANNUAL INTEREST IS BETWEEN " R-0.1; "AND " R; "PERCENT"
560 GOTO 5
570
800 REM ERROR ROUTINE
810 PRINT "INCORRECT DATA. PLEASE RETYPE."
820 GOTO 5
850 PRINT "ANSWER IS NEGATIVE."
860 GOTO 5
999
1000 REM COMPUTE BASIC QUANTITIES
1010 LET N = Y*T
1020 LET R1 = R/100/T
1030 LET P1 = P/R1
1040 LET R2 = (1+R1)^N
1099 RETURN
9999 END
 WARNING: Terminating statement in FOR/NEXT

